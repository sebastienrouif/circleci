import org.gradle.api.internal.plugins.DefaultExtraPropertiesExtension

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = buildNum

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitBranch() {
    def p = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitTimestamp() {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def setCrashlyticsParams(DefaultExtraPropertiesExtension extension) {
    def gitBranch = gitBranch()
    def group
    extension.betaDistributionReleaseNotes = "branch : ${gitBranch} | buildNum ${buildNum} | commit : ${gitSha()}"
    if ("master".equalsIgnoreCase(gitBranch)) {
        group = "release"
    } else if (gitBranch.startsWith("hotfix/") || gitBranch.startsWith("release/")) {
        group = "release-candidate"
    } else if ("develop".equalsIgnoreCase(gitBranch)) {
        group = "nightly"
    } else if (gitBranch.startsWith("feature/")) {
        group = "features"
    } else {
        throw new RuntimeException("You are not using git flow")
    }
    project.logger.info("distributing to group : " + group)
    extension.betaDistributionGroupAliases = group
}

static def renameArtifact(variant, defaultConfig) {
    variant.outputs.all { output ->
        def fullName = output.outputFile.name
        def projectName = fullName.substring(0, fullName.indexOf('-'))

        outputFileName = output.outputFile.name.replace(projectName, "${projectName}-${defaultConfig.versionName}")
    }
}


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.rouif.sebastien.circleci"
        minSdkVersion 19
        targetSdkVersion 28

        versionCode versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
        buildConfigField 'boolean', 'IS_CI', "${isCi}"
        resValue "string", "version_name", "v. ${versionName}"

        setCrashlyticsParams(ext)
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file("config/keystore/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    applicationVariants.all { variant -> renameArtifact(variant, defaultConfig) }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        debug {
            versionNameSuffix "-dev"
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation("com.crashlytics.sdk.android:crashlytics:2.8.0@aar") {
        transitive = true
    }

    testImplementation "junit:junit:4.12"

    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"

}
